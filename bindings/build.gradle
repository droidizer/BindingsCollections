def artifactName = "bindings"

ext.bintrayuser = System.getenv("BINTRAY_USER")
ext.bintraypwd = System.getenv("BINTRAY_PWD")

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven'
apply plugin: 'maven-publish'

apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk

        versionCode maven.artifactVersionCode
        versionName artifactName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding.enabled = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.kotlin.stdlib
    testImplementation 'junit:junit:4.12'
    implementation deps.aac.extensions
    implementation deps.aac.paging
    implementation deps.support.appcompat
    implementation deps.support.design

    implementation deps.rxjava.runtime
    implementation deps.rxjava.rxandroid
}
repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompile.classpath
        }
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file(getProperty('aar.deployPath'))
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId maven.artifactGroup
            version maven.artifactVersion
            artifactId artifactName
        }
    }
}

bintray {
    user = bintrayuser
    key = bintraypwd
    dryRun = false

    publications = ['Publication']
    pkg {
        repo = maven.repo
        userOrg = maven.userOrg
        websiteUrl = maven.siteUrl
        vcsUrl = maven.gitUrl
        licenses = maven.licenses
        name = artifactName
        publish = true

        version {
            name = maven.artifactVersion
            desc = maven.artifactDesc
            vcsTag = maven.artifactVersion
            released = new Date()
        }
    }
}

publishing {
    publications {
        Publication(MavenPublication) {
            artifact("$buildDir/outputs/aar/bindings-release.aar")
            artifact sourcesJar
            artifact javadocJar
            groupId maven.artifactGroup
            version maven.artifactVersion
            artifactId artifactName
        }
    }
}